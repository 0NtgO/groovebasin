{EventEmitter} = require 'events'

command_permissions =
  addid: \add
  clear: \control
  currentsong: \read
  deleteid: \control
  listallinfo: \read
  listplaylist: \read
  listplaylists: \read
  lsinfo: \read
  move: \control
  next: \control
  password: null
  pause: \control
  play: \control
  playid: \control
  playlistadd: \add
  playlistinfo: \read
  previous: \control
  repeat: \control
  seekid: \control
  setvol: \control
  shuffle: \control
  status: \read
  sticker: \admin
  stop: \control

function qEscape (str)
  # replace all " with \"
  str.toString().replace /"/g, '\\"'

const
  PLAYING = \playing
  PAUSED = \paused

module.exports = class PlayerServer extends EventEmitter
  (@library, @parser, @authenticate) ~>
    @parser.on \status, !(systems) ~>
      refresh_mpd_status = false
      client_systems = []
      for system of systems
        if system of <[stored_playlist sticker]>
          client_systems.push system
        else if system of <[playlist player mixer options]>
          refresh_mpd_status = true
      @emit \status, client_systems if client_systems.length
      @refreshMpdStatus! if refresh_mpd_status
    @parser.on \error, !(msg) ~>
      console.error "mpd error:", msg

    @playlist = {}
    @current_id = null
    @repeat = repeat: false, single: false
    @playing_state = PAUSED
    @seekto = null

    @parser.sendRequest "clear"
    @mpd_playlist = []
    @mpd_status =
      current_song: null
      state: \stop
      elapsed: null

  refreshMpdStatus: !->
    @parser.sendRequest \currentsong, (response) ~>
      @mpd_status.current_song = response
    @parser.sendRequest \status, (o) ~>
      @mpd_status.state = o.state
      @mpd_status.elapsed = if o.elapsed? then parseFloat o.elapsed else null
  createClient: (socket, permissions) ->
    # TODO: encapsulate client object
    client = socket
    client.permissions = permissions
    socket.on \request, !(request) ~>
      request = JSON.parse request
      @request client, request.cmd, !(arg) ~>
        response = JSON.stringify {callback_id: request.callback_id} <<< arg
        socket.emit \PlayerResponse, response
    # TODO: clean up these listeners when the socket disconnects
    @on \status, !(arg) ~> try socket.emit \PlayerStatus, JSON.stringify arg
    @on \error,  !(msg) ~> try socket.emit \MpdError, msg

    socket.emit 'Permissions', JSON.stringify(permissions)
    @on 'password', !(pass) ~>
      if success = (ref = @authenticate(pass))?
        client.permissions := ref
      socket.emit 'Permissions', JSON.stringify(client.permissions)
      socket.emit 'PasswordResult', JSON.stringify(success)

    return client

  request: !(client, request, cb=->) ->
    ~function check_permission (name)
      permission = command_permissions[name]
      return true if permission is null
      return true if client.permissions[permission]
      err = "command #{JSON.stringify name} requires permission #{JSON.stringify permission}"
      console.warn "permissions error:", err
      cb {err}
      return false

    if typeof request is 'object'
      # new-style command is an object
      {name} = request
      return unless check_permission name
      suppress_reply = false
      reply_object = null
      switch name
        case \listallinfo
          suppress_reply = true
          @library.get_library (library) -> cb {msg: library}
        case \password then @emit \password, request.password

        case \playlistinfo
          reply_object = @playlist
        case \currentsong
          reply_object = @current_id
        case \status
          reply_object =
            volume: null
            repeat: @repeat.repeat
            single: @repeat.single
            state: if @playing_state is PLAYING then 'play' else 'pause'
            elapsed: 0

        case \addid
          for {file, pos} of request.items
            @playlist[pos] = file
          @playlistChanged!
        case \deleteid
          for id of request.ids
            delete @playlist[id]
          @playlistChanged!
        case \move
          for {id, pos} of request.items
            @playlist[pos] = delete @playlist[id]
          @playlistChanged!
        case \clear
          @playlist = {}
          @playlistChanged!
        case \shuffle
          void # TODO

        case \repeat
          @repeat.repeat = request.repeat
          @repeat.single = request.single
        case \play
          @current_id ?= @playlist[0]?.id
          @playing_state = PLAYING
          @playlistChanged!
        case \pause
          if @playing_state is PLAYING
            @playing_state = PAUSED
          @playlistChanged!
        case \stop
          @playing_state = PAUSED
          @seekto = 0
          @playlistChanged!
        case \next
          @current_id = (@current_id ? -1) + 1
          @seekto = 0
          @playlistChanged!
        case \previous
          @current_id = (@current_id ? -1) - 1
          @seekto = 0
          @playlistChanged!
        case \playid
          @current_id = request.track_id
          @seekto = 0
          @playing_state = PLAYING
          @playlistChanged!
        default throw new Error "invalid command #{JSON.stringify name}"
      unless suppress_reply
        if reply_object? then cb {msg: reply_object} else cb {}

    else
      # old code still uses mpd string commands.
      # check permissions
      name = request.split(/\s/)[0]
      if name is 'command_list_begin'
        commands = request.split('\n')
        commands.shift!
        commands.pop!
        for command of commands
          name = command.split(/\s/)[0]
          return unless check_permission name
      else
        return unless check_permission name
      @parser.sendRequest request, cb

  playlistChanged: !->
    unless @playlist[@current_id]?
      # TODO look forward for the next song
      @current_id = null
      @playing_state = PAUSED
      @seekto = 0
    if @seekto?
      # TODO
      @seekto = null
    @parser.sendRequest "clear"
    if @current_id?
      @parser.sendRequest "addid \"#{qEscape @playlist[@current_id]}\""
    if @playing_state is PLAYING
      @parser.sendRequest "play"
    @emit \status, <[playlist player]>
