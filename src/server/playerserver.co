{EventEmitter} = require 'events'

command_permissions =
  addid: \add
  clear: \control
  currentsong: \read
  deleteid: \control
  listallinfo: \read
  listplaylist: \read
  listplaylists: \read
  lsinfo: \read
  move: \control
  next: \control
  password: null
  pause: \control
  play: \control
  playid: \control
  playlistadd: \add
  playlistinfo: \read
  previous: \control
  repeat: \control
  seekid: \control
  setvol: \control
  shuffle: \control
  status: \read
  sticker: \admin
  stop: \control

function qEscape (str)
  # replace all " with \"
  str.toString().replace /"/g, '\\"'

module.exports = class PlayerServer extends EventEmitter
  (@library, @parser, @authenticate) ~>
    # TODO: we are the only listeners to these emits. we should have more direct communication.
    @parser.on \status, !(arg) ~> @emit \status, arg
    @parser.on \error, !(msg) ~> @emit \error, msg

    @playlist = {}
    @current_id = null
    @repeat = repeat: false, single: false

  createClient: (socket, permissions) ->
    # TODO: encapsulate client object
    client = socket
    client.permissions = permissions
    socket.on \request, !(request) ~>
      request = JSON.parse request
      @request client, request.cmd, !(arg) ~>
        response = JSON.stringify {callback_id: request.callback_id} <<< arg
        socket.emit \PlayerResponse, response
    # TODO: clean up these listeners when the socket disconnects
    @on \status, !(arg) ~> try socket.emit \PlayerStatus, JSON.stringify arg
    @on \error,  !(msg) ~> try socket.emit \MpdError, msg

    socket.emit 'Permissions', JSON.stringify(permissions)
    @on 'password', !(pass) ~>
      if success = (ref = @authenticate(pass))?
        client.permissions := ref
      socket.emit 'Permissions', JSON.stringify(client.permissions)
      socket.emit 'PasswordResult', JSON.stringify(success)

    return client

  request: !(client, request, cb=->) ->
    ~function check_permission (name)
      permission = command_permissions[name]
      return true if permission is null
      return true if client.permissions[permission]
      err = "command #{JSON.stringify name} requires permission #{JSON.stringify permission}"
      console.warn "permissions error:", err
      cb {err}
      return false

    if typeof request is 'object'
      # new-style command is an object
      {name} = request
      return unless check_permission name
      switch name
        case \listallinfo then @library.get_library (library) -> cb {msg: library}
        case \password then @emit \password, request.password

        case \playlistinfo
          cb {msg: @playlist}
        case \currentsong
          cb {msg: @current_id}
        case \addid
          for {file, pos} of request.items
            @playlist[pos] = file
          @emitStatus client
        case \deleteid
          for id of request.ids
            delete @playlist[id]
          @emitStatus client
        case \move
          for {id, pos} of request.items
            @playlist[pos] = delete @playlist[id]
          @emitStatus client
        case \clear
          @playlist = {}
          @emitStatus client
        case \shuffle
          void # TODO

        case \repeat
          @repeat.repeat = request.repeat
          @repeat.single = request.single
        case \stop
          void # TODO
        case \play
          void # TODO
        case \pause
          void # TODO
        case \next
          void # TODO
        case \previous
          void # TODO
        case \playid
          @current_id = request.id
        default throw new Error "invalid command #{JSON.stringify name}"

    else
      # old code still uses mpd string commands.
      # check permissions
      name = request.split(/\s/)[0]
      if name is 'command_list_begin'
        commands = request.split('\n')
        commands.shift!
        commands.pop!
        for command of commands
          name = command.split(/\s/)[0]
          return unless check_permission name
      else
        return unless check_permission name
      @parser.sendRequest request, cb
  emitStatus: !->
    @emit \status, <[playlist player]>
