Plugin = require('../plugin')

const
  CHATS_LIMIT = 100
  USER_NAME_LIMIT = 20

module.exports = class Chat extends Plugin
  (bus) ~>
    super(...)
    # the online users list is always blank at startup
    @users = []

    bus.on 'save_state', @~saveState
    bus.on 'restore_state', @~restoreState
    bus.on 'socket_connect', @~onSocketConnection
    bus.on 'mpd', @~setMpd

  restoreState: !(state) ->
    @next_user_id = state.next_user_id ? 0
    @user_names = state.status.user_names ? {}
    @chats = state.status.chats ? []

  saveState: !(state) ->
    state.next_user_id = @next_user_id
    state.status.users = @users
    state.status.user_names = @user_names
    state.status.chats = @chats

  setMpd: !(@mpd) ->
    @mpd.on 'chat', @~scrubStaleUserNames

  onSocketConnection: !(socket) ->
    user_id = "user_" + @next_user_id
    @next_user_id += 1
    @users.push user_id
    socket.emit 'Identify', user_id
    socket.on 'Chat', !(data) ~>
      msg = data.toString()
      chat_object =
        user_id: user_id
        message: msg
      console.info "chat: #{@user_names[user_id]}: #{msg}"
      @chats.push(chat_object)
      @chats.splice(0, @chats.length - CHATS_LIMIT) if @chats.length > CHATS_LIMIT
      @emit('status_changed')
    socket.on 'SetUserName', !(data) ~>
      user_name = data.toString().trim().replace(/\s+/g, " ")
      if user_name is not ""
        user_name = user_name.substr(0, USER_NAME_LIMIT)
        @user_names[user_id] = user_name
      else
        delete @user_names[user_id]
      @emit('status_changed')
    socket.on 'disconnect', !~>
      @users = (id if id is not user_id for id of @users)
      @scrubStaleUserNames()
    @emit('status_changed')

  scrubStaleUserNames: !->
    keep_user_ids = {}
    for user_id of @users
      keep_user_ids[user_id] = true
    for chat_object of @chats
      keep_user_ids[chat_object.user_id] = true
    for user_id in @user_names
      delete @user_names[user_id] unless keep_user_ids[user_id]
    @emit('status_changed')

