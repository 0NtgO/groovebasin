fs = require 'fs'
http = require 'http'
net = require 'net'
socketio = require 'socket.io'
express = require 'express'
Mpd = require '../mpd.js/lib/mpd'
extend = require 'node.extend'
path = require 'path'

process.env.SETUID ||= ""
process.env.HOST ||= "0.0.0.0"
process.env.PORT ||= "16242"
process.env.MPD_CONF ||= "/etc/mpd.conf"
process.env.STATE_FILE ||= ".state.json"
process.env.NODE_ENV ||= "dev"
process.env.LASTFM_API_KEY ||= "7d831eff492e6de5be8abb736882c44d"
process.env.LASTFM_SECRET ||= "8713e8e893c5264608e584a232dd10a0"

arrayToObject = (array) ->
  obj = {}
  obj[item] = true for item of array
  obj

app = express()
app.configure ->
  app.use(express.static(path.join(__dirname, '../public')))
  app.use(express.static(path.join(__dirname, '../src/public')))

app_server = http.createServer(app)

io = socketio.listen(app_server)
io.set 'log level', 2
port = parseInt(process.env.PORT, 10)

app_server.listen port, process.env.HOST, ->
  process.send 'online'
  console.info "Listening at http://#{process.env.HOST}:#{port}"

process.on 'message', (message) ->
  if message is 'shutdown'
    process.exit(0)

# downgrade user permissions
try
  process.setuid uid if uid = process.env.SETUID
catch error
  console.error "error setting uid: #{error}"
console.info "server running as user #{process.getuid()}"


plugins =
  objects:
    lastfm: null
    dynamicmode: null
    upload: null
    download: null
    chat: null
    stream: null
    delete: null
  initialize: ->
    for name in this.objects
      {Plugin} = require("./plugins/#{name}")
      this.objects[name] = new Plugin(saveState, saveAndSend)
  call: (fn_name, ...args) ->
    plugin[fn_name](...args) for name, plugin in this.objects
  featuresList: ->
    ([name, plugin.is_enabled] for name, plugin in this.objects)

# state management
state =
  state_version: 2 # bump this whenever persistent state should be discarded
  status: {} # this structure is visible to clients

saveState = ->
  plugins.call "saveState", state
  fs.writeFile process.env.STATE_FILE, JSON.stringify(state, null, 4), "utf8"

restoreState = ->
  try loaded_state = JSON.parse fs.readFileSync process.env.STATE_FILE, "utf8"
  if loaded_state?.state_version is state.state_version
    extend true, state, loaded_state
  # have the plugins restore and then save to delete values that should not
  # have been restored.
  plugins.call "restoreState", state
  plugins.call "saveState", state

sendStatus = ->
  plugins.call "onSendStatus", state.status
  io.sockets.emit 'Status', JSON.stringify state.status

saveAndSend = ->
  saveState()
  sendStatus()

plugins.initialize()
plugins.call 'setUpRoutes', app

restoreState()

# read mpd conf
mpd_conf = null
root_pass = null
accounts = null
default_account = null
do ->
  mpd_conf_path = process.env.MPD_CONF
  try
    data = fs.readFileSync(mpd_conf_path)
  catch error
    console.warn "Unable to read #{mpd_conf_path}: #{error}. Most features disabled."
    return
  mpd_conf := require('./mpdconf').parse(data.toString())

  plugins.call "setConf", mpd_conf, mpd_conf_path

  if mpd_conf.auto_update is not "yes"
    console.warn "recommended to turn auto_update on in #{mpd_conf_path}"
  if mpd_conf.gapless_mp3_playback is not "yes"
    console.warn "recommended to turn gapless_mp3_playback on in #{mpd_conf_path}"
  if mpd_conf.volume_normalization is not "yes"
    console.warn "recommended to turn volume_normalization on in #{mpd_conf_path}"
  if isNaN(n = parseInt(mpd_conf.max_command_list_size)) or n < 16384
    console.warn "recommended to set max_command_list_size to >= 16384 in #{mpd_conf_path}"


  all_permissions = "read,add,control,admin"
  accountIsRoot = (account) ->
    for perm of all_permissions.split(',')
      if not account[perm]
        return false
    return true

  default_account := arrayToObject((mpd_conf.default_permissions ? all_permissions).split(","))
  if accountIsRoot(default_account)
    root_pass := ""
  accounts := {}
  for account_str of (mpd_conf.password ? [])
    [password, perms] = account_str.split("@")
    accounts[password] = account = arrayToObject(perms.split(","))
    if not root_pass? and accountIsRoot(account)
      root_pass := password

  if default_account.admin
    console.warn "Anonymous users have admin permissions. Recommended to remove `admin` from `default_permissions` in #{mpd_conf_path}"
  if not root_pass?
    rand_pass = Math.floor(Math.random() * 99999999999)
    console.error """
      It is required to have at least one password which is granted all the
      permissions. Recommended to add this line in #{mpd_conf_path}:

        password "groovebasin-#{rand_pass}@#{all_permissions}"

      """
    process.exit(1)

plugins.call "saveState", state

for [name, enabled] of plugins.featuresList()
  if enabled
    console.info "#{name} is enabled."
  else
    console.warn "#{name} is disabled."

createMpdConnection = (unix_socket, cb) ->
  if unix_socket and (path = mpd_conf?.bind_to_address?.unix_socket)?
    net.connect path, cb
  else
    port = mpd_conf?.port ? 6600
    host = mpd_conf?.bind_to_address?.network ? "localhost"
    net.connect port, host, cb

connectBrowserMpd = (socket) ->
  mpd_socket = createMpdConnection false, ->
    try socket.emit 'MpdConnect'
  mpd_socket.on 'data', (data) ->
    socket.emit 'FromMpd', data.toString()
  mpd_socket.on 'end', ->
    try socket.emit 'MpdDisconnect'
  mpd_socket.on 'error', ->
    console.warn "browser no mpd daemon found."

  socket.removeAllListeners 'ToMpd'
  socket.on 'ToMpd', (data) ->
    try mpd_socket.write data
  socket.removeAllListeners 'disconnect'
  socket.on 'disconnect', ->
    mpd_socket.end()

io.sockets.on 'connection', (socket) ->
  connectBrowserMpd socket
  permissions = default_account
  plugins.call "onSocketConnection", socket, -> permissions
  socket.emit 'Permissions', JSON.stringify(permissions)
  socket.on 'Password', (data) ->
    pass = data.toString()
    if success = (ref = accounts[pass])?
      permissions := ref
    socket.emit 'Permissions', JSON.stringify(permissions)
    socket.emit 'PasswordResult', JSON.stringify(success)

# our own mpd connection
class DirectMpd extends Mpd
  constructor: (@mpd_socket) ->
    super()
    @mpd_socket.on 'data', ~> @receive.apply(this, arguments)

  send: (data) ->
    try @mpd_socket.write data


my_mpd = null
my_mpd_socket = null
connect_success = true
connectServerMpd = ->
  my_mpd_socket := createMpdConnection true, ->
    console.info "server to mpd connect"
    connect_success := true
    my_mpd.handleConnectionStart()
    if root_pass? and root_pass.length > 0
      my_mpd.authenticate root_pass

    # connect socket clients to mpd
    io.sockets.clients().forEach connectBrowserMpd
  my_mpd_socket.on 'end', ->
    console.warn "server mpd disconnect"
    tryReconnect()
  my_mpd_socket.on 'error', ->
    if connect_success
      connect_success := false
      console.warn "server no mpd daemon found."
    tryReconnect()
  my_mpd := new DirectMpd(my_mpd_socket)
  my_mpd.on 'error', (msg) -> console.error msg

  plugins.call "setMpd", my_mpd

tryReconnect = ->
  setTimeout connectServerMpd, 1000

connectServerMpd()
