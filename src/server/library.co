path    = require 'path'
mutagen = require 'mutagen'
walk    = require 'walk'
{EventEmitter} = require 'events'

module.exports = class Library extends EventEmitter
  (@bus) ~>
    @bus.on 'save_state', @~onSaveState

  onSaveState: !(state) ->
    @music_lib_path = state.mpd_conf.music_directory
    @startScan!

  startScan: !->
    return if @library?
    @library = {}
    paths = []
    walker = walk.walk @music_lib_path, followLinks: false
    walker.on \file, !(root, fileStats, next) ~>
      paths.push path.join root, fileStats.name
      next!
    console.log 'starting library scan'
    start_time = new Date!.getTime!
    walker.on \end, !~>
      mutagen.read paths, !(err, tagses) ~>
        if err
          console.log err.stderr
          return
        for path, i of paths
          @library[path] = tagses[i]
        console.log "library scan complete. #{Math.round new Date!.getTime! - start_time}ms."
        @emit \library, @library
