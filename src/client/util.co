_exports = exports ? window.Util = {}

_exports.schedule = (delay, func) -> setInterval(func, delay * 1000)
_exports.wait = (delay, func) -> setTimeout(func, delay * 1000)

_exports.shuffle = !(array) ->
  top = array.length
  while --top > 0
    current = Math.floor(Math.random() * (top + 1))
    tmp = array[current]
    array[current] = array[top]
    array[top] = tmp

_exports.formatTime = (seconds) ->
  seconds = Math.floor seconds
  minutes = Math.floor seconds / 60
  seconds -= minutes * 60
  hours = Math.floor minutes / 60
  minutes -= hours * 60
  zfill = (n) ->
    if n < 10 then "0#n" else "#n"
  if hours is not 0
    return "#{hours}:#{zfill minutes}:#{zfill seconds}"
  else
    return "#{minutes}:#{zfill seconds}"


# converts any string into an HTML id, guaranteed to be unique
bad_char_re = new RegExp('[^a-zA-Z0-9\-]', 'gm')
_exports.toHtmlId = (string) ->
  string.replace bad_char_re, (c) -> "_#{c.charCodeAt(0)}_"

# compares 2 arrays with positive integers, returning > 0, 0, or < 0
_exports.compareArrays = (arr1, arr2) ->
  for val1, i1 of arr1
    val2 = arr2[i1]
    diff = (val1 ? -1) - (val2 ? -1)
    return diff if diff is not 0
  return 0

_exports.parseQuery = (query) ->
  obj = {}
  return obj unless query?

  for [param, val] of (valset.split('=') for valset of query.split('&'))
    obj[unescape(param)] = unescape(val)

  return obj

